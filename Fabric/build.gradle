plugins {
    alias fabric.plugins.loom
}

dependencies {
    minecraft libs.minecraft
    mappings(loom.layered {
        it.officialMojangMappings()
        it.parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.minecraft.get()}:${libs.versions.parchment.mappings.get()}@zip")
    })
    compileOnly project(":Common")

    modImplementation fabric.fabric.loader
    modImplementation fabric.fabric.api

    modImplementation libs.sparkweave.fabric
    modImplementation libs.resourcefulconfig.fabric

    modCompileOnly fabric.modmenu
    modLocalRuntime fabric.modmenu

    modCompileOnly "${libs.emi.fabric.get()}:api"
    modLocalRuntime libs.emi.fabric
}

repositories {
    mavenCentral()
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net'
    }
    maven {
        name = 'NeoForge'
        url = 'https://maven.neoforged.net/releases'
    }
    maven {
        name = "Sponge"
        url = "https://repo.spongepowered.org/repository/maven-public"
        content {
            includeGroupAndSubgroups "org.spongepowered"
        }
    }
    maven {
        name = "ParchmentMC"
        url = "https://maven.parchmentmc.org"
        content {
            includeGroupAndSubgroups "org.parchmentmc"
        }
    }
    maven {
        name = "Up-Mods"
        url = "https://maven.uuid.gg/releases"
    }
    maven {
        name = "TeamResourceful"
        url = "https://maven.teamresourceful.com/repository/maven-releases"
    }
    maven {
        name "TerraformersMC"
        url "https://maven.terraformersmc.com/releases"
    }
    maven {
        name "Modrinth"
        url "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

loom {
    mods {
        "${mod_id}" {
            sourceSet("main")
            project(":Common").sourceSets.main
        }
    }

    accessWidenerPath.set(file("src/main/resources/${mod_id}.accesswidener"))

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")

            if (rootProject.hasProperty('mc_uuid')) {
                programArgs("--uuid", rootProject.mc_uuid)
            }

            if (rootProject.hasProperty('mc_username')) {
                programArgs("--username", rootProject.mc_username)
            }

            if (rootProject.hasProperty('mc_java_agent_path')) {
                vmArg("-javaagent:${rootProject.mc_java_agent_path}")
            }
        }

        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run/server")
        }
    }
}

tasks.named("compileJava", JavaCompile) {
    source(project(":Common").sourceSets.main.allJava)
}

tasks.named("javadoc", Javadoc) {
    source(project(":Common").sourceSets.main.allJava)
}

tasks.named("sourcesJar", Jar) {
    from(project(":Common").sourceSets.main.allSource)
}

processResources {
    from project(":Common").sourceSets.main.resources
}
